/*******************************************************************************
 * Copyright (c) 2012 BMW Car IT and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *******************************************************************************/
package org.jnario.doc

import org.eclipse.xtend.lib.Property
import org.eclipse.xtext.xbase.lib.Procedures$Procedure1

class HtmlFile {

	public static HtmlFile EMPTY_FILE = new HtmlFile

	def static newHtmlFile(Procedures$Procedure1<HtmlFile> initializer){
		val htmlFile = new HtmlFile()
		initializer.apply(htmlFile)
		return htmlFile
	}
	
	@Property HtmlAssets assets = new HtmlAssets	
	@Property CharSequence name = ""
	@Property CharSequence title = ""
	@Property CharSequence content = ""
	@Property String rootFolder = ""
	@Property CharSequence sourceCode = ""
	@Property CharSequence fileName = ""
	
	def toText()'''
		<!DOCTYPE html>
		<html lang="en">
		<head>
		<meta charset="utf-8">
		<title>«title»</title>
		<meta name="description" content="">
		<meta name="author" content="Jnario">

		<!--[if lt IE 9]>
		      <script src="http://html5shim.googlecode.com/svn/trunk/html5.js"></script>
		    <![endif]-->

		«FOR cssFile : assets.cssFiles»
		<link rel="stylesheet" href="«rootFolder»«cssFile»">
		«ENDFOR»
		«FOR jsFile : assets.jsFiles»
		<script type="text/javascript" src="«rootFolder»«jsFile»"></script>
		«ENDFOR»
		</head>
		
		<body onload="prettyPrint()">
			<div class="container">
				<div class="tabbable">
					<div class="content">
						<div class="page-header">
							<h1>«title»</h1>
							  <ul class="nav nav-tabs pull-right">
							    <li class="active"><a href="#spec" data-toggle="tab">Spec</a></li>
								<li><a href="#source" data-toggle="tab">Source</a></li>
							  </ul>
						</div>
						<div class="row">
							<div class="span12">
								  <div class="tab-content">
									  	<div class="tab-pane active" id="spec">
		«content»
									</div>
								    <div class="tab-pane" id="source">
								    	<h3>«fileName»</h3>
								    	<p>
		«sourceCode»
								    </p></div>
								  </div>
							</div> 
						</div> <!-- /row -->
					</div> <!-- /content -->
				</div> <!-- /tabbable -->
				<footer>
					<p><small>Generated by <a href="http://www.jnario.org">Jnario</a>.</small></p>
				</footer>
			</div> <!-- /container -->
		
		</body>
		</html>
	'''
	
} 