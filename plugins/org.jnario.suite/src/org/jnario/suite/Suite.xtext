grammar org.jnario.suite.Suite with org.eclipse.xtend.core.Xtend

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://www.eclipse.org/xtext/common/JavaVMTypes" as types
import "http://www.eclipse.org/xtend" as xtend
import "http://www.eclipse.org/xtext/xbase/Xtype" as xtype
import "http://www.org/Jnario" as jnario
import "http://www.jnario.org/suite/Suite"


SuiteFile returns xtend::XtendFile: 
	{SuiteFile}
    ('package' package=QualifiedName ';'?)?
	importSection=XImportSection?
	(xtendTypes+=Suite)*;

Import returns xtype::XImportDeclaration: 
	'import'
		(importedType=[types::JvmDeclaredType|QualifiedName] 
		| importedNamespace=QualifiedNameWithWildcard) ';'?
;


Suite :
	{Suite}
	annotations+=XAnnotation* 
	name=SUITE_NAME
	elements+=Reference*
;

PatternReference:
	'-' pattern=PATTERN
;

Reference: 
	SpecReference | PatternReference
;

SpecReference:
	'-' spec=[jnario::Specification|STRING] text=TEXT?
;

QualifiedName:
  ValidID (=>'.' ValidID)*;

terminal SUITE_NAME: '#'+ !'-' ('\\' ('#'|'-') |!('-'|'#'))*;
terminal PATTERN: '\\'  !('\r'|'\n')* '\r'? '\n';
terminal TEXT: ':' ('\\' ('#'|'-') |!('-'|'#'))*  ;
terminal ID:
	'^'? ('a'..'z'|'A'..'Z'|'$'|'_') ('a'..'z'|'A'..'Z'|'$'|'_'|'0'..'9')*;

/**
 * Dummy rule, for "better" downwards compatibility, since GrammarAccess generates non-static inner classes, 
 * which makes downstream grammars break on classloading, when a rule is removed.
 */
StaticQualifier:
	(ValidID '::')+
;